Multichar_Symbols +NM +NF +NN +A +AMposs +AFposs +Masc +Mina +Fem +Neut +Sg +Du +Pl +Nom +Gen +Dat +Acc +Voc +Loc +Ins Sup+ +Pos +Comp @P.SUP.ON@ @D.SUP@

LEXICON Root

Noun;
Adj;

LEXICON Noun

NMyStem;
NMPléStem;
NMkStem;
NMlStem;
NMiStem;
NIyStem;
NIkStem;
NIiStem;
NFaStem;
NFeStem;
NFtStem;
NNoStem;
NNeStem;
NNteStem;
NNíStem;

LEXICON Adj

AyStem;
AiStem;

LEXICON NMyStem

běs NMy;
bratr NMy;
bratr+NM+Du+Gen:bratřú #;
bratr+NM+Pl+Nom:bratřie #;
bratr+NM+Pl+Gen:bratří #;
bratr+NM+Pl+Dat:bratří #;
bratr+NM+Pl+Acc:bratřie #;
červ         NMy;
červ:črv     NMy;
ďábel:ďábEl  NMy;
ďábel:diábEl NMy;
had NMy;
hospodin NMy;
kokot NMy;
kozel:kozEl NMy;
krajan+NM+Sg+Nom:krajěnín NMy;
krajan+NM+Pl+Nom:krajěné #;
krajan:krajěn NMy; ! ne Nom
mistr NMy;
pán NMy;
pes:pEs NMy;
pes+NM+Pl+Gen:psí #;
pop NMy;
satan NMy;
satan+NM+Sg+Voc:satanas #;
satan+NM+Sg+Voc:sathana #;
syn NMy;
velbloud:velblúd NMy;

! Osobní jména. Nemůžeme mít na začátku velké písmeno, protože třeba "E" používáme jako morfonologický signál.
abraham          NMy;
alfeus+NM+Sg+Nom:alfeus #;
alfeus+NM+Sg+Nom:alpheus #;
alfeus:alfe      NMy;
alfeus:alphe     NMy;
belzebub         NMy;
david            NMy;
herodes:herodes  NMy; ! Neděláme výjimku jen pro Nom Sg, protože je doloženo i "Herodesovi".
herodes:herod    NMy;
herodes:erodes   NMy;
herodes:erod     NMy;
jakub            NMy;
jan              NMy;
jezukristus+NM+Sg+Nom:jezukristus #;
jezukristus:jezukrist NMy;
josef            NMy;
josef:jozef      NMy;
josef:ozěp       NMy;
kristus+NM+Sg+Nom:kristus #;
kristus:krist    NMy;
nazareus+NM+Sg+Nom:nazareus #;
nazareus:nazare  NMy;
nazareus+NM+Sg+Nom:nazarenus #;
nazareus:nazaren NMy;
petr             NMy;
pilát            NMy;
šalamoun         NMy;
šalamoun:šalomún NMy;
zebedeus+NM+Sg+Nom:zebedeus #;
zebedeus:zebede  NMy;

LEXICON NMPléStem

pohan NMPlé;
žid NMPlé;

LEXICON NMkStem

bůh+NM+Sg+Nom:bóh #;
bůh+NM+Sg+Gen:božé #; ! Doloženo jako alternativa k "boha".
bůh:boh NMk;
bůh:buoh NMk;
býk NMk;
člověk NMk;
dělník NMk;
dlužník NMk;
duch NMk;
duchovník NMk;
hříšník:hřiešník NMk;
kozel:kozelEc NMi;
licoměrník NMk;
mládeneček:mládenečEk NMk;
mudrák NMk;
prorok NMk;
svědek:svědEk NMk;
učedník:učedlník NMk;
učedník:učenník NMk;
zjevník:zjěvník NMk;

LEXICON NMlStem

anděl NMl;
anděl:anjel NMl;
apoštol NMl;
přítel:přietel NMl;

! Osobní jména. Nemůžeme mít na začátku velké písmeno, protože třeba "E" používáme jako morfonologický signál.
daniel           NMl;
křtitel          NMl;
křtitel:krstitel NMl;
křtitel:křstitel NMl;

LEXICON NMiStem

císař:ciesař NMi;
kacíř:kacieř NMi;
kopáč        NMi;
muž          NMi;
muž+NM+Pl+Gen:muží #;
otec:otEc    NMi;
panoš        NMi;
panoš+NM+Sg+Nom:panošicě #;
panoš+NM+Pl+Nom:panošicě #;
panoš+NM+Sg+Ins:panoší #;
panoš+NM+Pl+Gen:panoší #;
panoš+NM+Pl+Ins:panošěmi #;
pisec:pisEc NMi;
rybář NMi;
rytíř:rytieř NMi;
šílenec:šielenEc NMi;
tovaryš:tovařiš NMi;
vodič NMi;
vodič+NM+Pl+Nom:vodičěvé #;
vodič+NM+Pl+Voc:vodičěvé #;
vrabec:vrabEc NMi;
žnec:žnEc NMi;
žnec:ženc NMi;

! Osobní jména. Nemůžeme mít na začátku velké písmeno, protože třeba "E" používáme jako morfonologický signál.
barnabáš          NMi;
barnabáš:barrabáš NMi;
eliáš             NMi;
eliáš:heliáš      NMi;
izaiáš            NMi;
ježíš             NMi;
mojžíš            NMi;
mojžíš:mojžieš    NMi;
zachař            NMi;
zebedeus:zebedáš  NMi;
zebedeus:zebedeáš NMi;

LEXICON NIyStem

! Neživotná tvrdá maskulina mají pár možností navíc, ale asi můžou využít i všechny životné tvary.

balšám                NIy;
čas                   NIy;
čin                   NIy;
den:dEn               NIy;
den+NM+Sg+Gen:dne     #;
den+NM+Pl+Gen:dní     #;
div                   NIy;
dům+NM+Sg+Nom:dóm     #;
dům:dom               NIy;
dům:duom              NIy;
hrob                  NIy;
chléb+NM+Sg+Nom:chléb #;
chléb:chleb           NIy;
kout                  NIy;
kout:kút              NIy;
národ                 NIy;
pas                   NIy;
peníz                 NIy;
peníz:peniez          NIy;
poklad                NIy;
příklad               NIy;
rov                   NIy;
sbor                  NIy;
sbor:sbuor            NIy;
sen:sEn               NIy;
sen+NM+Pl+Loc:snách   #;
stín:stien            NIy;
stůl+NM+Sg+Nom:stól   #;
stůl:stol             NIy;
soud:súd              NIy;
úd                    NIy;
vítr:větr             NIy;
vítr:vietr            NIy;
vlas                  NIy;
zub                   NIy;

! Vlastní jména. Nemůžeme mít na začátku velké písmeno, protože třeba "E" používáme jako morfonologický signál.
betlém           NIy;
betlém:bethlem   NIy;
betlém:bethleem  NIy;
jeruzalém        NIy;
jordán           NIy;
nazaret          NIy;
nazaret:nazareth NIy;

LEXICON NIkStem

fík              NIk;
hřích:hřiech     NIk;
okrajek:okrajEk  NIk;
ostatek:ostatEk  NIk;
ostatek:uostatEk NIk;
otrusek:otrusEk  NIk;
neduh            NIk;
podhrdlek:podhrdlEk NIk;
podolek:podolEk  NIk;
skutek:skutEk    NIk;
šťovík:ščěvík    NIk;
uzlík            NIk;
užitek:užitEk    NIk;
zárodek:zárodEk  NIk;
zbytek:zbytEk    NIk;

LEXICON NIiStem

! Neživotná měkká maskulina mají pár možností navíc, ale asi můžou využít i všechny životné tvary.

bochník:buochenEc NIi;
kořen       NIi;
koš         NIi;
koš:kuoš    NIi;
oheň:ohEň   NIi;
oheň:uohEň  NIi;
pláč        NIi;
plamen      NIi;
plášť       NIi;
plášť:plášč NIi;
tisíc       NIi;

LEXICON NFaStem

cesta:cěst   NFa;
dna:dn       NFa;
hora:hor     NFa;
hora:huor    NFa;
libra:libr   NFa;
lichva:lichv NFa;
matka:matk   NFa;
mísa:mís     NFa;
modlitba:modlitv NFa;
modlitba+NF+Pl+Gen:modlitv #; ! Doloženo i bez vloženého "e"
noha:noh         NFa;
noha+NF+Pl+Gen:nóh #;
potopa:potop     NFa;
přísaha:přísah   NFa;
ruka:ruk      NFa;
škola:škuol   NFa;
trouba:trúb   NFa;
vina:vinn     NFa;
víra:vier     NFa;
voda:vuod     NFa;
žena:žen      NFa;
! Zřejmě se velmi podobně skloňovala i mužská jména podle dnešního vzoru předseda.
sluha:sluh         NFa;
sluha:slúh         NFa;
starosta:starost   NFa;
tetrarcha:tetrarch NFa;

! Vlastní jména. Nemůžeme mít na začátku velké písmeno, protože třeba "E" používáme jako morfonologický signál.
sodoma:sodom       NFa;

LEXICON NFeStem

božnice:buožnic       NFe;
duše:duš              NFe;
dcera+NF+Sg+Nom:dci   #;
dcera+NF+Sg+Voc:dci   #;
máti+NF+Sg+Nom:mátě   #;
máti+NF+Sg+Acc:máteř  #;
máti+NF+Sg+Acc:mateř  #;
máti+NF+Sg+Gen:mateře #;
máti+NF+Sg+Gen:mateřě #;
máti+NF+Sg+Dat:mateři #;
máti+NF+Sg+Ins:mateří #;
máti+NF+Sg+Ins:mateřú #;
měřice:měřic          NFe;
ovce:ovc              NFe;
ovce:uovc             NFe;
světelnice:světedlnic NFe;
ulice:ulic            NFe;
vesnice:vesnic        NFe;
! Zřejmě se velmi podobně skloňovala i mužská jména podle vzoru súdcě.
soudce:súdc           NFe;

! Vlastní jména. Nemůžeme mít na začátku velké písmeno, protože třeba "E" používáme jako morfonologický signál.
dekapolis:dekapol      NFe;
marie:mari             NFe;
marie+NF+Sg+Nom:maria  #;
marie+NF+Sg+Nom:máří   #;
marie+NF+Sg+Ins:marijí #;
marie+NF+Sg+Ins:maří   #;
marie:maři             NFe;
marie:máři             NFe;

LEXICON NFtStem

poušť:púšč NFt;
poušť:púšť NFt;
rez        NFt;
síť:sieť   NFt;
sůl:sól    NFt;
sůl:sol    NFt;
sůl:suol   NFt;
stred      NFt;

LEXICON NNoStem

břicho:břich     NNo;
důstojenství:důstojenstv  NNo;
důstojenství:dóstojenstv  NNo;
důstojenství:duostojenstv NNo;
hnízdo:hnízd     NNo;
hnízdo:hniezd    NNo;
město:měst       NNo;
místo:míst       NNo;
místo:miest      NNo;
nebe:nebes       NNo;
písmeno:písmen   NNo;
právo:práv       NNo;
robátko:robátEk  NNo;
rok:let          NNo; ! Chtělo by to zablokovat singulár.
roucho:rouch     NNo;
roucho:rúch      NNo;
seno:sen         NNo;
seno:sěn         NNo;
slovo:slov       NNo;
ústa:úst         NNo; ! Chtělo by to zablokovat singulár.
ústa:ust         NNo;
zábradlí:zábradl NNo; ! zábradlách

LEXICON NNeStem

břímě:břím      NNe;
břímě:břiem     NNe;
dítě:dět        NNe;
dítě:diet       NNe;
nebe:neb        NNe;
písmeno:písemEc NNe; ! písemce
slovo:slovEc    NNe; ! slovce
srdce:srdEc     NNe;
tržiště:tržišť  NNe;
tržiště:tržišč  NNe;
vejce:vejc      NNe;
vejce:vajEc     NNe;

LEXICON NNteStem

dítě:dít      NNte;
dítě:diet     NNte;
oslíče:oslíč  NNte;
robě:rob      NNte;

LEXICON NNíStem

důstojenství:důstojenstv  NNí;
důstojenství:dóstojenstv  NNí;
důstojenství:duostojenstv NNí;
násilí:násil              NNí;
znamení:znameň            NNí; ! Kmen je měkký. Většinou "ň" zase zmizí díky "í". Ale Gen/Loc duálu je "znameňú".

LEXICON AyStem

dobrý@D.SUP@+Pos:dobr@D.SUP@ Ay;
dobrý@D.SUP@+Pos:dobreh@D.SUP@ Ay;
důstojný:důstojn     AyDeg;
důstojný:dóstojn     AyDeg;
důstojný:duostojn    AyDeg;
lesní:lesk           AyDeg;
lidský:lidsk         AyDeg;
malý@D.SUP@+Pos:mal@D.SUP@ Ay;
malý+Comp:menš       Ai;
mladý:mlad           Ay1Deg;
mladý+Comp:mlazš     Ai;
nebeský:nebesk       AyDeg;
ohnivý:ohniv         AyDeg;
ohnivý:ohněv         AyDeg;
popový:popov         AyDeg;
propuštěný:propuščen AyDeg;
smrtelný:smrtedln    AyDeg;
starý:star           Ay1Deg;
veliký@D.SUP@+Pos:velik@D.SUP@ Ay;
veliký+Comp:větš     Ai;
vysoký@D.SUP@+Pos:vysok@D.SUP@ Ay;
vysoký+Comp:vyšš     Ai;
zelený:zelen         AyDeg;
židovský:židovsk     AyDeg;

! Co se jmennými tvary adjektiv?
bílý:biel          Ax;
černý:črn          Ax;
důstojný:důstojEn  Ax;
důstojný:dóstojEn  Ax;
důstojný:duostojEn Ax;
hotový:hotov       Ax;
lačný:lačEn        Ax;
mocný:mocEn        Ax;
nahý:náh           Ax;
náměsíčný:náměséčEn Ax;
nemocný:nemocEn    Ax;
nicí:nic           Ax;
plný:pln           Ax;
podobný:podobEn    Ax;
poslušný:poslušEn  Ax;
povinný:povinEn    Ax;
smutný:smutEn      Ax;
vinný:vinEn        Ax;
známý:znám         Ax;
živý:živ           Ax;
živý:žív           Ax;
žíznivý:žiezniv    Ax;
žíznivý:žiezliv    Ax;

LEXICON AiStem

bližní@D.SUP@+Pos:bližn@D.SUP@ Ai;
boží:bož          AiDeg;
boží:buož         AiDeg;
člověčí:člověč    AiDeg;
jarní:jarn        AiDeg;
ještěrčí:ještěrč  AiDeg;
ještěrčí:ješčerč  AiDeg;
ještěrčí:jěščerč  AiDeg;
ještěrčí:ješčeřič AiDeg;
ještěrčí:jěščeřič AiDeg;
volající@D.SUP@+Pos:volajíc@D.SUP@ Ai;
volající@D.SUP@+Pos:vuolajíc@D.SUP@ Ai;

!------------------------------------------------------------------------------
! Konec kmenů, začátek koncovek.
!------------------------------------------------------------------------------

LEXICON NM

! Zdá se, že základní mužské skloňování je tvrdé a jde ho použít pro všechna podstatná jména,
! jejichž nominativ končí na souhlásku. Některé lexémy navíc umožňují další měkké koncovky,
! které se pro ně v pozdějších fázích češtiny stanou povinné. Ale ve staročeských textech se
! u nich, byť ne často, objevují i tvrdé koncovky, třeba dativ "mužu". Zde uvedené se nevztahuje
! na podstatná jména, jejichž nominativ končí samohláskou. Našel jsem nicméně dvě tvrdé koncovky,
! které zřejmě pro měkká jména použít nejde Acc a Ins Pl -y.

+NM+Sg+Nom:^λ    #; ! anděl přietel běs bóh dělník duch had hospodin kacieř kopáč kokot muž otec pán pop tovařiš
! Gen a Acc Sg jsou vždy stejné.
+NM+Sg+Gen:^a    #; ! anděla přietela běsa boha dělníka ducha hada hospodina kacieřa kopáča kokota muža otca pána popa tovařiša
+NM+Sg+Acc:^a    #;
! Dat a Loc Sg jsou vždy stejné.
+NM+Sg+Dat:^u    #; ! andělu   přietelu   běsu   bohu   dělníku   duchu   hadu   hospodinu   kacieřu   kopáču   kokotu   mužu   otcu   pánu   popu   tovařišu
+NM+Sg+Dat:^ovi  #; ! andělovi přietelovi běsovi bohovi dělníkovi duchovi hadovi hospodinovi kacieřovi kopáčovi kokotovi mužovi otcovi pánovi popovi tovařišovi
+NM+Sg+Loc:^u    #;
+NM+Sg+Loc:^ovi  #;
! Ale narazil jsem i na Loc "hospodině", "velblúdě" (Dat zatím nedávám, dokud ho taky nepotkám).
! A pravděpodobně se nebude vyskytovat po některých souhláskách, např. "bohě" by nejspíš bylo "bozě", pokud vůbec.
+NM+Sg+Loc:^ě    #;
! Voc se zřejmě liší podle kmene. Koncovka -e vypadá relativně nejrozšířenější, ale u měkkých jmen je problematická kvůli kolizi s genitivem.
! Musíme rozpoznat vokativní ^e (měkčí kmenovou souhlásku, otec-otče) od genitivního ^e (neměkčí, otec-otce).
+NM+Sg+Voc:^E    #; ! anděle přietele běse bože dělníče duše  hade hospodine kacieře kopáče kokote muže otče páne pope tovařiše
+NM+Sg+Ins:^em   #; ! andělem přietelem běsem bohem dělníkem duchem hadem hospodinem kacieřem kopáčem kokotem mužem otcem pánem popem tovařišem
! Duál je vzácný a jeho koncovky zatím nemám dostatečně prozkoumané.
+NM+Du+Gen:^ú    #;
+NM+Du+Loc:^ú    #;
+NM+Du+Dat:^oma  #;
+NM+Du+Ins:^oma  #;
! Nom a Voc Pl jsou vždy stejné.
+NM+Pl+Nom:^i    #; ! anděli   přietel?   běsi   bozi   dělníci   duši    hadi   hospodini   kacieři   kopáči   kokoti   muži   otci   páni   popi   tovařiši
+NM+Pl+Nom:^ové  #; ! andělové přietelové běsové bohové dělníkové duchové hadové hospodinové kacieřové kopáčové kokotové mužové otcové pánové popové tovařišové
+NM+Pl+Voc:^i    #;
+NM+Pl+Voc:^ové  #;
+NM+Pl+Nom:^ie   #; ! attested: bratřie črvie mužie tovařišie židie
+NM+Pl+Voc:^ie   #;
! Gen a Dat Pl mají jednoznačné koncovky (-ó- vs. -uo- je jen starší vs. mladší varianta) pro všechna mužská jména.
! Gen Pl se často používal v přímém předmětu místo Acc, ale to nehodnotím jako alternativní tvar akuzativu, nýbrž jako partitivní konstrukci (nejsem si jist, že je to OK).
+NM+Pl+Gen:^ů    #;
+NM+Pl+Gen:^óv   #; ! andělóv  přietelóv  běsóv  bohóv  dělníkóv  duchóv  hadóv  hospodinóv  kacieřóv  kopáčóv  kokotóv  mužóv  otcóv  pánóv  popóv  tovařišóv
+NM+Pl+Gen:^uov  #; ! anděluov přieteluov běsuov bohuov dělníkuov duchuov haduov hospodinuov kacieřuov kopáčuov kokotuov mužuov otcuov pánuov popuov tovařišuov
+NM+Pl+Dat:^ům   #;
+NM+Pl+Dat:^óm   #; ! andělóm  přietelóm  běsóm  bohóm  dělníkóm  duchóm  hadóm  hospodinóm  kacieřóm  kopáčóm  kokotóm  mužóm  otcóm  pánóm  popóm  tovařišóm
+NM+Pl+Dat:^uom  #; ! anděluom přieteluom běsuom bohuom dělníkuom duchuom haduom hospodinuom kacieřuom kopáčuom kokotuom mužuom otcuom pánuom popuom tovařišuom
+NM+Pl+Loc:^ech  #;
+NM+Pl+Loc:^iech #;

! Dále lze vytvořit přivlastňovací přídavné jméno (hlavně od životných, ale nemusíme to omezovat).
AdjMposs;

LEXICON NMy

! Mužské tvrdé skloňování.

NM; ! Kromě níže uvedených lze použít i všechny koncovky ze základního slovníku NM.
+NM+Du+Nom:^y    #;
+NM+Du+Acc:^y    #;
+NM+Du+Voc:^y    #;
+NM+Pl+Acc:^y    #; ! anděly běsy bohy dělníky duchy hady hospodiny kokoty pány popy
+NM+Pl+Ins:^y    #;

LEXICON NMPlé

! Mužské tvrdé skloňování s alternativním plurálem na -é.
! (Zde nejsou "andělé", protože "anděl" má ještě navíc alternativní vokativ na -i.)

NMy; ! Kromě níže uvedených lze použít i všechny koncovky ostatních tvrdých jmen.
+NM+Pl+Nom:^é    #; ! pohané židé
+NM+Pl+Voc:^é    #; ! pohané židé

LEXICON NMk

! Mužské tvrdé skloňování jmen končících na -h, -ch, -k.

NMy; ! Kromě níže uvedených lze použít i všechny koncovky ostatních tvrdých jmen.
! Voc se zřejmě liší podle kmene.
+NM+Sg+Voc:^u    #; ! bohu dělníku duchu

LEXICON NMl

! Mužské obojetné skloňování některých jmen končících na -l.

NMy; ! Kromě níže uvedených lze použít i všechny koncovky ze základního slovníku NM a z tvrdého NMy.
! Voc se zřejmě liší podle kmene.
+NM+Sg+Voc:^i    #; ! anděli přieteli běs? boh? dělník? duch? had? hospodin? kacieři kopáči kokot? muži otc? pán? pop? tovařiši
+NM+Pl+Nom:^é    #; ! andělé   přietelé   běs?   boh?   dělník?   duch?   had?
+NM+Pl+Voc:^é    #;

LEXICON NMi

! Mužské měkké skloňování.

NM; ! Kromě níže uvedených lze použít i všechny koncovky ze základního slovníku NM.
! Gen a Acc Sg jsou vždy stejné.
+NM+Sg+Gen:^e    #; ! anděle přietele běse bož? dělní?? du??? hadě hospodině kacieře kopáče kokot? muže otce páně pop? tovařiše
+NM+Sg+Acc:^e    #;
! Dat a Loc Sg jsou vždy stejné.
+NM+Sg+Dat:^i    #; ! anděli   přieteli   běs?   boh?   dělník?   duch?   had?   hospodin?   kacieři   kopáči   kokot?   muži   otci   pán?   pop?   tovařiši
+NM+Sg+Loc:^i    #;
+NM+Sg+Ins:^iem  #; ! Není mi jasné, která jména umožňují koncovku -iem. Doloženo je kopáčiem, rytieřiem.
+NM+Du+Nom:^ě    #; ! rybářě
+NM+Du+Gen:^í    #; ! pod stráži dvú rytieří
+NM+Du+Acc:^ě    #;
+NM+Du+Voc:^ě    #;
+NM+Pl+Gen:^iev  #; ! kacieřiev kopáčiev tovařišiev ; košiev kuošiev (doloženo)
+NM+Pl+Acc:^e    #; ! kacieře kopáče muže otce tovařiše
+NM+Pl+Ins:^i    #; ! kacieři kopáči muži otci tovařiši

LEXICON NIy

! Neživotná tvrdá maskulina mají pár možností navíc, ale asi můžou využít i všechny životné tvary.

NMy;
+NM+Sg+Acc:^λ #; ! Acc Sg bez koncovky jako Nom
+NM+Pl+Nom:^y #; ! Nom Pl jako Acc
+NM+Pl+Voc:^y #; ! Voc Pl jako Acc

LEXICON NIk

! Neživotná tvrdá maskulina mají pár možností navíc, ale asi můžou využít i všechny životné tvary.

NMk;
+NM+Sg+Acc:^λ #; ! Acc Sg bez koncovky jako Nom
+NM+Pl+Nom:^y #; ! Nom Pl jako Acc
+NM+Pl+Voc:^y #; ! Voc Pl jako Acc

LEXICON NIi

! Neživotná měkká maskulina mají pár možností navíc, ale asi můžou využít i všechny životné tvary.

NMi;
+NM+Sg+Acc:^λ #; ! Acc Sg bez koncovky jako Nom
+NM+Pl+Nom:^e #; ! Nom Pl jako Acc
+NM+Pl+Voc:^e #; ! Voc Pl jako Acc

LEXICON NFa

+NF+Sg+Nom:^a   #;
+NF+Sg+Gen:^y   #;
+NF+Sg+Dat:^e   #;
+NF+Sg+Dat:^ě   #; ! Ve staré češtině se vyskytuje i po měkkých souhláskách, takže nestačí měnit -e na -ě pomocí fonologických pravidel.
+NF+Sg+Acc:^u   #;
+NF+Sg+Voc:^o   #;
+NF+Sg+Loc:^e   #;
+NF+Sg+Loc:^ě   #; ! Ve staré češtině se vyskytuje i po měkkých souhláskách, takže nestačí měnit -e na -ě pomocí fonologických pravidel.
+NF+Sg+Ins:^ú   #;
+NF+Sg+Ins:^ou  #;
+NF+Du+Nom:^ě   #;
+NF+Du+Acc:^ě   #;
+NF+Du+Voc:^ě   #;
+NF+Pl+Nom:^y   #;
+NF+Pl+Gen:^λ   #;
+NF+Pl+Dat:^ám  #;
+NF+Pl+Acc:^y   #;
+NF+Pl+Voc:^y   #;
+NF+Pl+Loc:^ách #;
+NF+Pl+Ins:^ami #;

LEXICON NFe

+NF+Sg+Nom:^ě    #;
+NF+Sg+Nom:^e    #;
+NF+Sg+Gen:^ě    #;
+NF+Sg+Gen:^e    #;
+NF+Sg+Dat:^i    #;
+NF+Sg+Acc:^i    #;
+NF+Sg+Acc:^u    #; ! Doložen tvar "dušu".
+NF+Sg+Voc:^ě    #;
+NF+Sg+Voc:^e    #;
+NF+Sg+Loc:^i    #;
+NF+Sg+Ins:^í    #;
+NF+Sg+Ins:^ú    #; ! Doložen tvar "dušú".
+NF+Pl+Nom:^ě    #;
+NF+Pl+Nom:^e    #;
+NF+Pl+Gen:^í    #;
+NF+Pl+Gen:^λ    #; ! Je to doloženo?
+NF+Pl+Dat:^iem  #;
+NF+Pl+Dat:^ím   #;
+NF+Pl+Acc:^ě    #;
+NF+Pl+Acc:^e    #;
+NF+Pl+Voc:^ě    #;
+NF+Pl+Voc:^e    #;
+NF+Pl+Voc:^ie   #; ! Doložen vokativ "súdcie".
+NF+Pl+Loc:^iech #;
+NF+Pl+Loc:^ích  #;
+NF+Pl+Ins:^ěmi  #;
+NF+Pl+Ins:^emi  #;

LEXICON NFt

+NF+Sg+Nom:^λ    #; ! púšč púšť
+NF+Sg+Nom:^ě    #; ! púščě
+NF+Sg+Gen:^ě    #; ! púščě
+NF+Sg+Gen:^e    #; ! púšče
+NF+Sg+Dat:^i    #;
+NF+Sg+Acc:^λ    #; ! púšč púšť
+NF+Sg+Acc:^i    #; ! púšči
+NF+Sg+Acc:^u    #; ! púšťu
+NF+Sg+Voc:^ě    #;
+NF+Sg+Loc:^i    #; ! púšči púšti
+NF+Sg+Ins:^í    #;
+NF+Pl+Nom:^ě    #;
+NF+Pl+Gen:^í    #;
+NF+Pl+Dat:^iem  #;
+NF+Pl+Acc:^ě    #;
+NF+Pl+Voc:^ě    #;
+NF+Pl+Loc:^iech #;
+NF+Pl+Ins:^ěmi  #;

LEXICON NNo

+NN+Sg+Nom:^o    #;
+NN+Sg+Gen:^a    #;
+NN+Sg+Dat:^u    #;
+NN+Sg+Acc:^o    #;
+NN+Sg+Voc:^o    #;
+NN+Sg+Loc:^u    #;
+NN+Sg+Loc:^e    #;
+NN+Sg+Loc:^ě    #;
+NN+Sg+Ins:^em   #;
+NN+Du+Nom:^ě    #;
+NN+Du+Nom:^e    #;
+NN+Du+Gen:^ú    #;
+NN+Du+Dat:^oma  #;
+NN+Du+Acc:^ě    #;
+NN+Du+Acc:^e    #;
+NN+Du+Voc:^ě    #;
+NN+Du+Voc:^e    #;
+NN+Du+Loc:^ú    #;
+NN+Du+Ins:^oma  #;
+NN+Pl+Nom:^a    #;
+NN+Pl+Gen:^λ    #;
+NN+Pl+Dat:^óm   #;
+NN+Pl+Dat:^uom  #;
+NN+Pl+Dat:^ům   #;
+NN+Pl+Acc:^a    #;
+NN+Pl+Voc:^a    #;
+NN+Pl+Loc:^ách  #;
+NN+Pl+Loc:^iech #;
+NN+Pl+Loc:^ech  #;
+NN+Pl+Ins:^y    #;
+NN+Pl+Ins:^ami  #;

LEXICON NNe

+NN+Sg+Nom:^e   #;
+NN+Sg+Nom:^ě   #;
+NN+Sg+Gen:^e   #;
+NN+Sg+Gen:^ě   #;
+NN+Sg+Dat:^i   #;
+NN+Sg+Dat:^u   #; ! Doloženo srdcu
+NN+Sg+Acc:^e   #;
+NN+Sg+Acc:^ě   #;
+NN+Sg+Voc:^e   #;
+NN+Sg+Voc:^ě   #;
+NN+Sg+Loc:^i   #;
+NN+Sg+Loc:^u   #; ! Doloženo srdcu
+NN+Sg+Ins:^em  #;
+NN+Du+Nom:^ě   #;
+NN+Du+Nom:^e   #;
+NN+Du+Gen:^ú   #;
+NN+Du+Dat:^oma #;
+NN+Du+Acc:^ě   #;
+NN+Du+Acc:^e   #;
+NN+Du+Voc:^ě   #;
+NN+Du+Voc:^e   #;
+NN+Du+Loc:^ú   #;
+NN+Du+Ins:^oma #;
+NN+Pl+Nom:^e   #;
+NN+Pl+Nom:^ě   #;
+NN+Pl+Gen:^í   #;
+NN+Pl+Gen:^λ   #; ! Doloženo srdec
+NN+Pl+Dat:^ím  #;
+NN+Pl+Acc:^e   #;
+NN+Pl+Acc:^ě   #;
+NN+Pl+Voc:^e   #;
+NN+Pl+Voc:^ě   #;
+NN+Pl+Loc:^ích #;
+NN+Pl+Ins:^i   #;

LEXICON NNte

+NN+Sg+Nom:^e     #;
+NN+Sg+Nom:^ě     #;
+NN+Sg+Gen:^ete   #;
+NN+Sg+Gen:^ěte   #;
+NN+Sg+Dat:^eti   #;
+NN+Sg+Dat:^ěti   #;
+NN+Sg+Acc:^e     #;
+NN+Sg+Acc:^ě     #;
+NN+Sg+Voc:^e     #;
+NN+Sg+Voc:^ě     #;
+NN+Sg+Loc:^eti   #;
+NN+Sg+Loc:^ěti   #;
+NN+Sg+Ins:^etem  #;
+NN+Sg+Ins:^ětem  #;
+NN+Pl+Nom:^ata   #;
+NN+Pl+Gen:^at    #;
+NN+Pl+Dat:^atům  #;
+NN+Pl+Dat:^atóm  #;
+NN+Pl+Dat:^atuom #;
+NN+Pl+Acc:^ata   #;
+NN+Pl+Voc:^ata   #;
+NN+Pl+Loc:^atech #;
+NN+Pl+Ins:^aty   #;

LEXICON NNí

+NN+Sg+Nom:^í   #;
+NN+Sg+Nom:^ie  #;
+NN+Sg+Gen:^í   #;
+NN+Sg+Dat:^í   #;
+NN+Sg+Acc:^í   #;
+NN+Sg+Acc:^ie  #;
+NN+Sg+Voc:^í   #;
+NN+Sg+Voc:^ie  #;
+NN+Sg+Loc:^í   #;
+NN+Sg+Ins:^ím  #;
+NN+Du+Nom:^í   #;
+NN+Du+Gen:^ú   #; ! znameňú
+NN+Du+Dat:^íma #; ! znameníma
+NN+Du+Acc:^í   #;
+NN+Du+Voc:^í   #;
+NN+Du+Loc:^ú   #; ! znameňú
+NN+Du+Ins:^íma #; ! znameníma
+NN+Pl+Nom:^í   #;
+NN+Pl+Nom:^é   #; ! násilé
+NN+Pl+Gen:^í   #;
+NN+Pl+Dat:^ím  #;
+NN+Pl+Acc:^í   #;
+NN+Pl+Acc:^é   #; ! násilé
+NN+Pl+Voc:^í   #;
+NN+Pl+Loc:^ích #;
+NN+Pl+Ins:^ími #;

LEXICON AdjMposs

+AMposs+Masc+Sg+Nom:^óv    #;
+AMposs+Masc+Sg+Nom:^uov   #;
+AMposs+Masc+Sg+Nom:^ův    #;
+AMposs+Masc+Sg+Gen:^ova   #;
+AMposs+Masc+Sg+Dat:^ovu   #;
+AMposs+Masc+Sg+Acc:^ova   #;
+AMposs+Masc+Sg+Voc:^óv    #;
+AMposs+Masc+Sg+Voc:^uov   #;
+AMposs+Masc+Sg+Voc:^ův    #;
+AMposs+Masc+Sg+Loc:^ově   #;
+AMposs+Masc+Sg+Ins:^ovým  #;
+AMposs+Masc+Pl+Nom:^ovi   #;
+AMposs+Masc+Pl+Gen:^ových #;
+AMposs+Masc+Pl+Dat:^ovým  #;
+AMposs+Masc+Pl+Acc:^ovy   #;
+AMposs+Masc+Pl+Voc:^ovi   #;
+AMposs+Masc+Pl+Loc:^ových #;
+AMposs+Masc+Pl+Ins:^ovými #;

+AMposs+Fem+Sg+Nom:^ova    #;
+AMposs+Fem+Sg+Gen:^ovy    #;
+AMposs+Fem+Sg+Dat:^ově    #;
+AMposs+Fem+Sg+Acc:^ovu    #;
+AMposs+Fem+Sg+Voc:^ova    #;
+AMposs+Fem+Sg+Loc:^ově    #;
+AMposs+Fem+Sg+Ins:^ovú    #;
+AMposs+Fem+Sg+Ins:^ovou   #;
+AMposs+Fem+Pl+Nom:^ovy    #;
+AMposs+Fem+Pl+Gen:^ových  #;
+AMposs+Fem+Pl+Dat:^ovým   #;
+AMposs+Fem+Pl+Acc:^ovy    #;
+AMposs+Fem+Pl+Voc:^ovy    #;
+AMposs+Fem+Pl+Loc:^ových  #;
+AMposs+Fem+Pl+Ins:^ovými  #;

+AMposs+Neut+Sg+Nom:^ovo   #;
+AMposs+Neut+Sg+Gen:^ova   #;
+AMposs+Neut+Sg+Dat:^ovu   #;
+AMposs+Neut+Sg+Acc:^ovo   #;
+AMposs+Neut+Sg+Voc:^ovo   #;
+AMposs+Neut+Sg+Loc:^ově   #;
+AMposs+Neut+Sg+Ins:^ovým  #;
+AMposs+Neut+Pl+Nom:^ova   #;
+AMposs+Neut+Pl+Gen:^ových #;
+AMposs+Neut+Pl+Dat:^ovým  #;
+AMposs+Neut+Pl+Acc:^ova   #;
+AMposs+Neut+Pl+Voc:^ova   #;
+AMposs+Neut+Pl+Loc:^ových #;
+AMposs+Neut+Pl+Ins:^ovými #;

LEXICON AdjSup

@P.SUP.ON@Sup+:@P.SUP.ON@nej Adj;

LEXICON AyDeg

@D.SUP@+Pos:@D.SUP@     Ay;
+Comp:^ejš Ai;

LEXICON Ay1Deg

@D.SUP@+Pos:@D.SUP@     Ay;
+Comp:^š   Ai;

LEXICON AiDeg

@D.SUP@+Pos:@D.SUP@     Ai;
+Comp:^ejš Ai;

LEXICON Ay

+A+Masc+Sg+Nom:^ý   #;
+A+Masc+Sg+Gen:^ého #;
+A+Masc+Sg+Dat:^ému #;
+A+Masc+Sg+Acc:^ého #;
+A+Masc+Sg+Voc:^ý   #;
+A+Masc+Sg+Loc:^ém  #;
+A+Masc+Sg+Ins:^ým  #;
+A+Masc+Pl+Nom:^í   #;
+A+Masc+Pl+Gen:^ých #;
+A+Masc+Pl+Dat:^ým  #;
+A+Masc+Pl+Acc:^é   #;
+A+Masc+Pl+Voc:^í   #;
+A+Masc+Pl+Loc:^ých #;
+A+Masc+Pl+Ins:^ými #;

+A+Mina+Sg+Nom:^ý   #;
+A+Mina+Sg+Gen:^ého #;
+A+Mina+Sg+Dat:^ému #;
+A+Mina+Sg+Acc:^ý   #;
+A+Mina+Sg+Voc:^ý   #;
+A+Mina+Sg+Loc:^ém  #;
+A+Mina+Sg+Ins:^ým  #;
+A+Mina+Pl+Nom:^é   #;
+A+Mina+Pl+Gen:^ých #;
+A+Mina+Pl+Dat:^ým  #;
+A+Mina+Pl+Acc:^é   #;
+A+Mina+Pl+Voc:^é   #;
+A+Mina+Pl+Loc:^ých #;
+A+Mina+Pl+Ins:^ými #;

+A+Fem+Sg+Nom:^á   #;
+A+Fem+Sg+Gen:^é   #;
+A+Fem+Sg+Gen:^éj  #;
+A+Fem+Sg+Dat:^é   #;
+A+Fem+Sg+Dat:^éj  #;
+A+Fem+Sg+Acc:^ou  #;
+A+Fem+Sg+Acc:^ú   #;
+A+Fem+Sg+Voc:^á   #;
+A+Fem+Sg+Loc:^é   #;
+A+Fem+Sg+Loc:^éj  #;
+A+Fem+Sg+Ins:^ou  #;
+A+Fem+Sg+Ins:^ú   #;
+A+Fem+Pl+Nom:^é   #;
+A+Fem+Pl+Gen:^ých #;
+A+Fem+Pl+Dat:^ým  #;
+A+Fem+Pl+Acc:^é   #;
+A+Fem+Pl+Voc:^é   #;
+A+Fem+Pl+Loc:^ých #;
+A+Fem+Pl+Ins:^ými #;

+A+Neut+Sg+Nom:^é   #;
+A+Neut+Sg+Gen:^ého #;
+A+Neut+Sg+Dat:^ému #;
+A+Neut+Sg+Acc:^é   #;
+A+Neut+Sg+Voc:^é   #;
+A+Neut+Sg+Loc:^ém  #;
+A+Neut+Sg+Ins:^ým  #;
+A+Neut+Pl+Nom:^á   #;
+A+Neut+Pl+Gen:^ých #;
+A+Neut+Pl+Dat:^ým  #;
+A+Neut+Pl+Acc:^á   #;
+A+Neut+Pl+Voc:^á   #;
+A+Neut+Pl+Loc:^ých #;
+A+Neut+Pl+Ins:^ými #;

LEXICON Ax

+Ax+Masc+Sg+Nom:^λ #;
+Ax+Masc+Sg+Acc:^a #;
+Ax+Masc+Pl+Nom:^i #;
+Ax+Masc+Pl+Acc:^y #;

+Ax+Mina+Sg+Nom:^λ #;
+Ax+Mina+Sg+Acc:^λ #;
+Ax+Mina+Pl+Nom:^y #;
+Ax+Mina+Pl+Acc:^y #;

+Ax+Fem+Sg+Nom:^a #;
+Ax+Fem+Sg+Acc:^u #;
+Ax+Fem+Pl+Nom:^y #;
+Ax+Fem+Pl+Acc:^y #;

+Ax+Neut+Sg+Nom:^o #;
+Ax+Neut+Sg+Acc:^o #;
+Ax+Neut+Pl+Nom:^a #;
+Ax+Neut+Pl+Acc:^a #;

LEXICON Ai

+A+Masc+Sg+Nom:^í    #;
+A+Masc+Sg+Gen:^ího  #;
+A+Masc+Sg+Gen:^ieho #;
+A+Masc+Sg+Dat:^ímu  #;
+A+Masc+Sg+Dat:^iemu #;
+A+Masc+Sg+Acc:^ího  #;
+A+Masc+Sg+Acc:^ieho #;
+A+Masc+Sg+Voc:^í    #;
+A+Masc+Sg+Loc:^ím   #;
+A+Masc+Sg+Ins:^ím   #;
+A+Masc+Pl+Nom:^í    #;
+A+Masc+Pl+Gen:^ích  #;
+A+Masc+Pl+Dat:^ím   #;
+A+Masc+Pl+Acc:^í    #;
+A+Masc+Pl+Voc:^í    #;
+A+Masc+Pl+Loc:^ích  #;
+A+Masc+Pl+Ins:^ími  #;

+A+Mina+Sg+Nom:^í    #;
+A+Mina+Sg+Gen:^ího  #;
+A+Mina+Sg+Gen:^ieho #;
+A+Mina+Sg+Dat:^ímu  #;
+A+Mina+Sg+Dat:^iemu #;
+A+Mina+Sg+Acc:^í    #;
+A+Mina+Sg+Voc:^í    #;
+A+Mina+Sg+Loc:^ím   #;
+A+Mina+Sg+Ins:^ím   #;
+A+Mina+Pl+Nom:^í    #;
+A+Mina+Pl+Gen:^ích  #;
+A+Mina+Pl+Dat:^ím   #;
+A+Mina+Pl+Acc:^í    #;
+A+Mina+Pl+Voc:^í    #;
+A+Mina+Pl+Loc:^ích  #;
+A+Mina+Pl+Ins:^ími  #;

+A+Fem+Sg+Nom:^í   #;
+A+Fem+Sg+Gen:^í   #;
+A+Fem+Sg+Gen:^ie  #;
+A+Fem+Sg+Dat:^í   #;
+A+Fem+Sg+Dat:^iej #;
+A+Fem+Sg+Acc:^í   #;
+A+Fem+Sg+Acc:^ú   #; ! Doloženo "božú" když je jinak "boží"
+A+Fem+Sg+Voc:^í   #;
+A+Fem+Sg+Loc:^í   #;
+A+Fem+Sg+Loc:^iej #;
+A+Fem+Sg+Ins:^í   #;
+A+Fem+Pl+Nom:^í   #;
+A+Fem+Pl+Gen:^ích #;
+A+Fem+Pl+Dat:^ím  #;
+A+Fem+Pl+Acc:^í   #;
+A+Fem+Pl+Acc:^ie  #;
+A+Fem+Pl+Voc:^í   #;
+A+Fem+Pl+Loc:^ích #;
+A+Fem+Pl+Ins:^ími #;

+A+Neut+Sg+Nom:^í    #;
+A+Neut+Sg+Nom:^ie   #;
+A+Neut+Sg+Gen:^ího  #;
+A+Neut+Sg+Gen:^ieho #;
+A+Neut+Sg+Dat:^ímu  #;
+A+Neut+Sg+Dat:^iemu #;
+A+Neut+Sg+Acc:^í    #;
+A+Neut+Sg+Acc:^ie   #;
+A+Neut+Sg+Voc:^í    #;
+A+Neut+Sg+Loc:^ím   #;
+A+Neut+Sg+Loc:^iem  #; ! "o božiem umučení"
+A+Neut+Sg+Ins:^ím   #;
+A+Neut+Pl+Nom:^í    #;
+A+Neut+Pl+Gen:^ích  #;
+A+Neut+Pl+Dat:^ím   #;
+A+Neut+Pl+Acc:^í    #;
+A+Neut+Pl+Voc:^í    #;
+A+Neut+Pl+Loc:^ích  #;
+A+Neut+Pl+Ins:^ími  #;
