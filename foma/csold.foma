# matk + ^0 --> matek, modlitv + ^0 --> modlitev
define NFPlGenEInsertion [t k]->[t e k], [t v]->[t e v] || _ "^" λ;

# otEc^λ -> otec, otEc^e -> otce
define NMEc  [E c]->[e c] || _ "^" λ;
define NMEce [E c]->c     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# svědEk^λ -> svědek, svědEk^a -> svědka
define NMEk  [E k]->[e k] || _ "^" λ;
define NMEka [E k]->k     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# diábEl^λ -> diábel, diábEl^a -> diábla (ale ne např. anjela -> anjla)
define NMEl  [E l]->[e l] || _ "^" λ;
define NMEla [E l]->l     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# dEn^λ -> den, dEn^e -> dne
define NMEn  [E n]->[e n] || _ "^" λ;
define NMEna [E n]->n     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# ohEň^λ -> oheň, ohEň^e -> ohňe
define NMEň  [E ň]->[e ň] || _ "^" λ;
define NMEňa [E ň]->ň     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# pEs^λ -> pes, pEs^a -> psa
define NMEs  [E s]->[e s] || _ "^" λ;
define NMEsa [E s]->s     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];

# bohe -> bože, duche -> duše, dělníke -> dělníče, mistre -> mistře, otce -> otče
# "h" nesmí následovat po "c" (protože pak je to "ch")
# Toto pravidlo se nesmí použít na koncovku -e, která není mužským vokativem, např. na genitiv "otce".
define NMSgVocPalatalization h->ž, [c h]->š, k->č, r->ř, c->č || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ "^" E .#.;

# bohi -> bozi, duchi -> duši, dělníki -> dělníci, bratri -> bratři
# bohie -> bozie, duchie -> dušie, dělníkie -> dělnície
# "h" nesmí následovat po "c" (protože pak je to "ch")
define NMPlNomPalatalizationXi [c h]->š, k->c, r->ř || _ "^" i;
define NMPlNomPalatalizationHi h->z || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ "^" i;

# bratré -> bratřé (ale ne židé -> žiďé, pohané -> pohaňé!)
# "é" je potřeba kvůli mužskému plurálu nominativu bratré -> bratřé
# "h" nesmí následovat po "c" (protože pak je to "ch")
define NMPlNomPalatalizationXé [c h]->š, k->c, r->ř || _ "^" é .#.;
define NMPlNomPalatalizationHé h->z || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ "^" é .#.;
define NMPlNomPalatalization NMPlNomPalatalizationXi .o. NMPlNomPalatalizationHi .o. NMPlNomPalatalizationXé .o. NMPlNomPalatalizationHé;

# duhe -> duze, archa -> arše, matke -> matce, hore -> hoře, pravde -> pravďe, ceste -> cesťe, žene -> žeňe
# dóstojnejší -> dóstojnější
# "h" nesmí následovat po "c" (protože pak je to "ch")
define NFSgDatPalatalizationX [c h]->š, k->c, r->ř, d->ď, t->ť, n->ň || _ "^" [e|ě] .#., _ "^" [e|ě] j š;
define NFSgDatPalatalizationH h->z || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ "^" [e|ě] .#.;
define NFSgDatPalatalization NFSgDatPalatalizationX .o. NFSgDatPalatalizationH;

# nahí -> nazí, plachí -> plaší, vysokí -> vysocí, dobrí -> dobří, nebeskí -> nebeští
# vrhích -> vrzích, vrchích -> vrších, fíkích -> fících, bratrích -> bratřích, pyskích -> pyscích (ne pyštích!)
# Nejsem si jist, co dělat s příznakovou diakritikou. Kazí mi kontext.
define APlNomPalatalizationX [c h]->š, r->ř, [s k]->[š t] || _ ("@D.SUP@") "^" í .#.;
define NPlLocPalatalizationX [c h]->š, r->ř               || _             "^" í c h .#.;
define APlNomPalatalizationH h->z || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ ("@D.SUP@") "^" í .#.,
                                     [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ "^" í c h .#.;
define APlNomPalatalizationK k->c || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|c|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|š|t|ť|v|w|x|z|ž] _ ("@D.SUP@") "^" í .#.,
                                                                                                                       _ "^" í c h .#.;
define APlNomPalatalization APlNomPalatalizationX .o. APlNomPalatalizationH .o. APlNomPalatalizationK .o. NPlLocPalatalizationX;

# ďe ťe ňe -> dě tě ně, ďi ťi ňi -> di ti ni
define DeTeNe [ď "^" e]->[d "^" ě], [ď "^" ě]->[d "^" ě], [ť "^" e]->[t "^" ě], [ť "^" ě]->[t "^" ě], [ň "^" e]->[n "^" ě], [ň "^" ě]->[n "^" ě], [ď "^" i]->[d "^" i], [ď "^" í]->[d "^" í], [ť "^" i]->[t "^" i], [ť "^" í]->[t "^" í], [ň "^" i]->[n "^" i], [ň "^" í]->[n "^" í];

# Finally erase temporary symbols.
define Surface "^" -> 0, λ -> 0, E -> e;

read lexc csold.lexc
define Lexicon;
regex Lexicon .o. NMEc .o. NMEce .o. NMEk .o. NMEka .o. NMEl .o. NMEla .o. NMEn .o. NMEna .o. NMEň .o. NMEňa .o. NMEs .o. NMEsa .o. NFPlGenEInsertion
    .o. NMSgVocPalatalization .o. NMPlNomPalatalization .o. NFSgDatPalatalization .o. APlNomPalatalization .o. DeTeNe .o. Surface;

print dot > fst.dot
save stack > fst.bin
