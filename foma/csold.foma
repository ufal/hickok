# Morfonologické převodníky pro starou češtinu.
# Obsahují v podstatě dva druhy pravidel, ale v různých variantách:
# - palatalizace (změna souhlásky na konci kmene)
# - vkládání -e- do souhláskových skupin

# Poznámka: s "ch" pracujeme jako se dvěma znaky, protože ve slovníku není definováno jako multichar symbol.
# Kvůli tomu ale musíme být opatrní u pravidel, která se mají vztahovat na "h".

#------------------------------------------------------------------------------
# Palatalizace
#------------------------------------------------------------------------------
# V různých situacích dochází k podobným, ale ne stejným hláskovým změnám.
# Většinou jde o změnu souhlásky na konci kmene, ta může být doplněna nebo
# nahrazena změnou kvality samohlásky (e->ě, y->i). V závislosti na fázi vývoje
# češtiny (a na nářečí) může jít o skutečnou změnu výslovnosti, nebo už pouze
# o její otisk v pravopisu.

# Typ 1: pádová koncovka -e/ě, souhlásky h:z, ch:š, k:c, r:ř, d:ď, t:ť, n:ň
# (Ve starších textech se -ě vyskytuje i po souhláskách, po kterých se později
# píše už jen -e: ž, š, č, ř, c, j, s, z.)
# - Masc Sg Gen: pán-páně, pokoj-pokojě, pláč-pláčě, muž-mužě
# - Masc Sg Loc: bůh-bozě, vrch-vršě, příbytek-přiebytcě, klášter-klášteřě, západ-západě, svět-světě, zákon-zákoně, hrob-hrobě, houf-houfě, chrám-chrámě, sklep-sklepě, ostrov-ostrově, les-lesě (dnes lese), peníz-peniezě (dnes peníze) pokoj-pokojě (dnes pokoje)
# - Fem Sg Dat/Loc: dráha-drázě, útěcha-útěšě, hora-hořě, pravda-pravdě, cesta-cestě, rodina-rodině, osoba-osobě, šífa-šífě, zima-zimě, potopa-potopě, hlava-hlavě, krása-krásě (dnes kráse), hrůza-hrózě (dnes hrůze)
# - Neut Sg Dat/Loc: roucho-rúšě, jitro-jitřě, pivo-pivě, maso-masě (dnes mase), železo-železě (dnes železe)
# - Nevztahuje se na některé delší koncovky, např. na Masc/Neut Ins -em: klášterem, nikoli klášteřem.
# - Nevztahuje se na koncovku -e pro Masc Voc. Tam dochází k jiným změnám, popsaným samostatně.
define PalXě [c h]->š, k->c, r->ř, d->ď, t->ť, n->ň || _ "^" [e|ě] .#.;
define PalHě h->z || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ "^" [e|ě] .#.;

# Typ 2: koncovka množného čísla -i, souhlásky h:z, ch:š, k:c, r:ř, d:ď, t:ť, n:ň
# - Masc Pl Nom/Voc: vrah-vrazi, duch-duši, rak-raci, bratr-bratři, had-hadi, kat-kati, pán-páni (u d/t/n dochází ke změně ve výslovnosti, ale ne v textu)
# - Vztahuje se i na starší delší koncovku -ie: bůh-bozie, duch-dušie, dělník-dělnície, bratr-bratřie.
# - Vztahuje se i na Pl Gen -iev, ale tato koncovka je méně častá, běžnější je -óv, -uov.
# - Vztahuje se i na Pl Dat -iem: bůh-boziem, duch-dušiem, bratr-bratřiem.
# - Vztahuje se i na Pl Loc -iech: bůh-boziech, duch-dušiech, bratr-bratřiech.
define PalXi [c h]->š, k->c, r->ř || _ "^" i;
define PalHi h->z || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ "^" i;

# Typ 3: koncovka množného čísla -é, souhlásky h:z, ch:š, k:c, r:ř
# - Masc Pl Nom/Voc: bratr-bratřé (ale ne žid-žiďé, kat-kaťé, pohan-pohaňé)
define PalXé [c h]->š, k->c, r->ř || _ "^" é .#.;
define PalHé h->z || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ "^" é .#.;

# Typ 4: koncovka vokativu -e, souhlásky h:ž, ch:š, k:č, r:ř, c:č
# - Masc Sg Voc: bůh-bože, duch-duše, dělník-dělníče, mistr-mistře, otec-otče (ale ne žid-židě atd.)
# - Toto pravidlo se nesmí použít na koncovku -e, která není mužským vokativem, např. na genitiv "otce".
# - Používám lexikální symbol E pro odlišení této koncovky od ostatních morfů začínajících na -e.
define PalXVoc [c h]->š, k->c, r->ř, c->č || _ "^" E .#.;
define PalHVoc h->ž || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ "^" E .#.;

# Typ 5: pádová koncovka začínající na -í, souhlásky h:z, ch:š, k:c, r:ř
# - Masc Pl Gen: bratr-bratří
# - Masc Pl Loc: vrh-vrzích, vrch-vrších, fík-fících, bratr-bratřích
# - Toto by mohlo zabrat i na množné číslo adjektiv, tady ale vzniká problém.
#   U adjektiv se má navíc uplatnit změna sk:šť, ck:čť, u substantiv ji ale nechceme (pysk-pyscích, nikoli pištích).
#   Naopak u adjektiv je tato změna povinná (český-čeští, nikoli čescí).
# - K identifikaci adjektiv zkusíme využít příznak @D.SUP@, který mají adjektiva (všechna?) ze slovníku, takže
#   bychom ho stejně museli zohlednit v kontextu. Neuděláme ho tedy v závorkách (volitelný), ale povinný. Viz níže.
define PalXí [c h]->š, k->c, r->ř || _ "^" í;
define PalHí h->z || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ "^" í;

# Typ 6: koncovka množného čísla adjektiv -í, souhlásky h:z, ch:š, k:c, r:ř, sk:šť, ck:čť
# - Adj Masc Pl Nom: nahý-nazí, plachý-plaší, vysoký-vysocí, dobrý-dobří, nebeský-nebeští, zámecký-zámečtí
# - K identifikaci adjektiv zkusíme využít příznak @D.SUP@, který mají adjektiva (všechna?) ze slovníku, takže
#   bychom ho stejně museli zohlednit v kontextu. Neuděláme ho tedy v závorkách (volitelný), ale povinný.
define PalXíAdj [c h]->š, r->ř, [s k]->[š ť], [c k]->[č ť] || _ "@D.SUP@" "^" í .#.;
define PalHíAdj h->z || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ "@D.SUP@" "^" í .#.;
define PalKíAdj k->c || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|š|t|ť|v|w|x|z|ž] _ "@D.SUP@" "^" í .#.;

# Typ 7: komparativní přípona -ějš/ejš, souhlásky h:z, ch:š, k:c, r:ř, d:ď, t:ť, n:ň, sk:šť, ck:čť
# - Adj Comp: důstojný-dóstojnější
# Jde sice o adjektiva, ale komparativ příznak @D.SUP@ neobsahuje.
define PalXějš  [c h]->š, r->ř, d->ď, t->ť, n->ň, [s k]->[š ť], [c k]->[č ť] || _ "^" [e|ě] j š;
define PalHějš  h->z || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|s|š|t|ť|v|w|x|z|ž] _ "^" [e|ě] j š;
define PalKějš  k->c || [a|á|e|é|ě|i|í|o|ó|u|ů|ú|y|ý|b|č|d|ď|f|g|h|j|k|l|m|n|ň|p|q|r|ř|š|t|ť|v|w|x|z|ž] _ "^" [e|ě] j š;

define Palatalization PalXě .o. PalHě .o. PalXi .o. PalHi .o. PalXé .o. PalHé .o. PalXVoc .o. PalHVoc .o. PalXí .o. PalHí
                      .o. PalXíAdj .o. PalHíAdj .o. PalKíAdj .o. PalXějš .o. PalHějš .o. PalKějš;

#------------------------------------------------------------------------------
# Vkládání -e-
#------------------------------------------------------------------------------

# prosEb^λ -> proseb, prosEb^a -> prosba
define InsEb  [E b]->[e b] || _ "^" λ;
define InsEba [E b]->b     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# otEc^λ -> otec, otEc^e -> otce
define InsEc  [E c]->[e c] || _ "^" λ;
define InsEce [E c]->c     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# svědEk^λ -> svědek, svědEk^a -> svědka; též matka-matek
define InsEk  [E k]->[e k] || _ "^" λ;
define InsEka [E k]->k     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# diábEl^λ -> diábel, diábEl^a -> diábla (ale ne např. anjela -> anjla)
define InsEl  [E l]->[e l] || _ "^" λ;
define InsEla [E l]->l     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# palEm^λ -> palem, palEm^a -> palma
define InsEm  [E m]->[e m] || _ "^" λ;
define InsEma [E m]->m     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# dEn^λ -> den, dEn^e -> dne
define InsEn  [E n]->[e n] || _ "^" λ;
define InsEna [E n]->n     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# ohEň^λ -> oheň, ohEň^e -> ohňe
define InsEň  [E ň]->[e ň] || _ "^" λ;
define InsEňa [E ň]->ň     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# libEr^λ -> liber, libEr^a -> libra
define InsEr  [E r]->[e r] || _ "^" λ;
define InsEra [E r]->r     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# pEs^λ -> pes, pEs^a -> psa
define InsEs  [E s]->[e s] || _ "^" λ;
define InsEsa [E s]->s     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# karEt^λ -> karet, karEt^a -> karta
define InsEt  [E t]->[e t] || _ "^" λ;
define InsEta [E t]->t     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];
# barEv^λ -> barev, barEv^a -> barva
define InsEv  [E v]->[e v] || _ "^" λ;
define InsEva [E v]->v     || _ "^" [a|á|e|é|ě|E|i|í|o|ó|u|ů|ú|y|ý];

define EInsertion InsEb .o. InsEba .o. InsEc .o. InsEce .o. InsEk .o. InsEka .o. InsEl .o. InsEla .o. InsEm .o. InsEma .o. InsEn .o. InsEna .o. InsEň .o. InsEňa
                  .o. InsEr .o. InsEra .o. InsEs .o. InsEsa .o. InsEt .o. InsEta .o. InsEv .o. InsEva;

#------------------------------------------------------------------------------
# Kombinace pravidel.
#------------------------------------------------------------------------------

# Úklidové pravopisné pravidlo: Pokud někde zůstalo [ďťň] před švem a [eěií] za ním, opravit.
# ďe ťe ňe -> dě tě ně, ďi ťi ňi -> di ti ni
define DeTeNe [ď "^" e]->[d "^" ě], [ď "^" ě]->[d "^" ě], [ť "^" e]->[t "^" ě], [ť "^" ě]->[t "^" ě], [ň "^" e]->[n "^" ě], [ň "^" ě]->[n "^" ě],
              [ď "^" i]->[d "^" i], [ď "^" í]->[d "^" í], [ť "^" i]->[t "^" i], [ť "^" í]->[t "^" í], [ň "^" i]->[n "^" i], [ň "^" í]->[n "^" í];

# Závěrečný úklid: zbavit se pomocných slovníkových symbolů.
define Surface "^" -> 0, λ -> 0, E -> e;

# Načíst slovník a zkombinovat to vše dohromady.
read lexc csold.lexc
define Lexicon;
regex Lexicon .o. EInsertion .o. Palatalization .o. DeTeNe .o. Surface;

# Uložit převodník ve formátu .dot (pro případnou vizualizaci) a .bin (pro použití s flookup).
# dot fst.dot -Tpng -o fst.png & start fst.png
# foma -l compile.foma ; ./generovat_tvary.pl marie NF | perl -CDSA ./flookup2perl.pl Maria
# (Poznámka: compile.foma volá tento skript, ale po uložení zásobníku z něj zase hned vyskočí.)
print dot > fst.dot
save stack > fst.bin
